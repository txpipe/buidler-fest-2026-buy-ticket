party Buyer;

party Issuer;

party Treasury;

env {
    issuer_beacon_policy: Bytes,
    issuer_beacon_name: Bytes,
    ticket_policy: Bytes,
    issuer_script_ref: UtxoRef,
}

type IssuerState {
    ticket_counter: Int,
}

tx buy_ticket(
    ticket_name: Bytes
) {
    locals {
        // blind ticket price
        ticket_price: 400000000,
        
        // normal ticket price
        // ticket_price: 500000000,

        ticket_token: AnyAsset(ticket_policy, ticket_name, 1),
        issuer_beacon: AnyAsset(issuer_beacon_policy, issuer_beacon_name, 1),
    }

    validity {
        // txs will be valid to submit from when they are created +10 mins
        until_slot: tip_slot() + 600,
    }

    reference issuer_script {
        ref: issuer_script_ref,
    }

    input* funds {
        from: Buyer,
        min_amount: fees + Ada(ticket_price) + min_utxo(ticket),
    }

    input current_state {
        from: Issuer,
        min_amount: issuer_beacon,
        datum_is: IssuerState,
        redeemer: (),
    }
    
    mint {
        amount: ticket_token,
        redeemer: (),
    }

    output ticket {
        to: Buyer,
        amount: funds - fees - Ada(ticket_price) + ticket_token,
    }

    output new_state {
        to: Issuer,
        amount: current_state,
        datum: IssuerState {
            ticket_counter: current_state.ticket_counter + 1,
        },
    }

    output payment {
        to: Treasury,
        amount: Ada(ticket_price),
    }

    collateral {
        from: Buyer,
        min_amount: fees,
    }
}
