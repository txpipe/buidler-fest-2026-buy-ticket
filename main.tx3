party Buyer;

party Ticketer;

party Treasury;

env {
    admin_token_policy: Bytes,
    admin_token_name: Bytes,
    ticket_policy: Bytes,
    ticketer_script: UtxoRef,
}

type TicketerDatum {
    ticket_counter: Int,
    ticket_policy: Bytes,
}

type TicketerRedeemer {
    BuyTicket,
}

type TicketPolicyRedeemer {
    MintTicket,
}

tx buy_ticket(
    ticket_name: Bytes, // TICKET + number
) {
    locals {
        new_ticket: AnyAsset(ticket_policy, ticket_name, 1),
        admin_token: AnyAsset(admin_token_policy, admin_token_name, 1),
        ticket_price: 10000000,
    }

    reference TicketerRef {
        ref: ticketer_script,
    }

    input* payment {
        from: Buyer,
        min_amount: fees + Ada(ticket_price) + min_utxo(payment_change),
    }

    input ticketer {
        from: Ticketer,
        min_amount: admin_token,
        datum_is: TicketerDatum,
        redeemer: TicketerRedeemer::BuyTicket {},
    }
    
    mint {
        amount: new_ticket,
        redeemer: TicketPolicyRedeemer::MintTicket {},
    }

    output updated_ticketer {
        to: Ticketer,
        amount: ticketer,
        datum: TicketerDatum {
            ticket_counter: ticketer.ticket_counter + 1,
            ticket_policy: ticketer.ticket_policy,
        },
    }

    output pay_for_ticket {
        to: Treasury,
        amount: Ada(ticket_price),
    }

    output payment_change {
        to: Buyer,
        amount: payment - fees - Ada(ticket_price) + new_ticket,
    }

    collateral {
        from: Buyer,
        min_amount: fees,
    }
}
