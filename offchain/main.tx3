party Buyer;

party Ticketer;

party Beneficiary;

env {
    admin_token_policy: Bytes,
    admin_token_name: Bytes,
    ticket_policy: Bytes,
}

type TicketerDatum {
    ticket_counter: Int,
    ticket_policy: Bytes,
}

type TicketerRedeemer {
    BuyTicket,
}

type TicketPolicyRedeemer {
    MintTicket,
}

tx buy_ticket(
    ticket_name: Bytes, // TICKET + number
) {
    locals {
        new_ticket: AnyAsset(ticket_policy, ticket_name, 1),
        admin_token: AnyAsset(admin_token_policy, admin_token_name, 1),
        ticket_price: 10000000,

        ticketer_script_ref: 0xd836c5493b2e3f95e1f1db8cc9c83b00c399f582dcb799ab4061b421e0513693#0,
    }

    // validity {
    //     until_slot: tip,
    // }

    reference TicketerRef {
        ref: ticketer_script_ref,
    }

    input* gas {
        from: Buyer,
        min_amount: fees + Ada(ticket_price) + min_utxo(ticket_owner),
    }

    input ticketer {
        from: Ticketer,
        min_amount: admin_token,
        datum_is: TicketerDatum,
        redeemer: TicketerRedeemer::BuyTicket {},
    }
    
    mint {
        amount: new_ticket,
        redeemer: TicketPolicyRedeemer::MintTicket {},
    }

    output ticket_owner {
        to: Buyer,
        amount: new_ticket + min_utxo(ticket_owner),
    }

    output updated_ticketer {
        to: Ticketer,
        amount: ticketer,
        datum: TicketerDatum {
            ticket_counter: ticketer.ticket_counter + 1,
            ticket_policy: ticketer.ticket_policy,
        },
    }

    output pay_for_ticket {
        to: Beneficiary,
        amount: Ada(ticket_price),
    }

    output gas_change {
        to: Buyer,
        amount: gas - fees - Ada(ticket_price) - min_utxo(ticket_owner),
    }

    collateral {
        from: Buyer,
        min_amount: fees,
    }
}
